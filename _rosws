#compdef rosws

zstyle ':completion::complete:rosws:*:descriptions' format '%B%d%b'
zstyle ':completion::complete:rosws:*:commands' group-name commands
zstyle ':completion::complete:rosws:*:workspaces_list' group-name workspaces_list
zstyle ':completion::complete:rosws::' list-grouped

zmodload zsh/mapfile

function _rosws() {
  local ROSWS_CONFIG=${ROSWS_CONFIG:-$HOME/.config/ros2-env/workspaces}
  local ret=1

  local -a commands
  local -a workspaces_list=( "${(f)mapfile[$ROSWS_CONFIG]//$HOME/~}" )
  local -a ros_distros=(
    ardent
    bouncy
    crystal
    dashing
    eloquent
    foxy
    galactic
    humble
    iron
    jazzy
    rolling
  )

  typeset -A rosws_workspaces
  while read -r line
  do
    local arr=(${(s,:,)line})
    ws_name=${arr[1]}
    ws_distro=${arr[2]}
    ws_path=${arr[3]}

    # replace ~ from path to fix completion (#17)
    ws_path=${ws_path/#\~/$HOME}

    rosws_workspaces[$ws_name]=$ws_path
  done < $ROSWS_CONFIG

  commands=(
    'activate:Set the given workspace as the active one'
    'distro:Set and source the given ROS 2 distro (humble, iron, rolling, etc.)'
    'add:Adds the current working directory to your registered workspaces'
    'rm:Removes the given workspace'
    'list:Print all registered workspaces'
    'show:Print info of given workspace (name and path)'
    'path:Show the path to given workspace (pwd)'
    'clean:Remove workspaces pointing to non-existent directories (will prompt unless --force is used)'
    'cd:Change directory to the workspace. You can also cd to a dir inside the workspace'
    'help:Show this extremely helpful text'
  )

  _arguments -C \
    '1: :->first_arg' \
    '2: :->second_arg' \
    '3: :->third_arg' && ret=0

  local command=$words[2]
  local ws_name=$words[3]

  case $state in
    first_arg)
      _describe -t workspaces_list "Workspaces" workspaces_list && ret=0
      _describe -t commands "Commands" commands && ret=0
      ;;
    second_arg)
      case $command in
        add\!|rm)
          _describe -t rosws_workspaces "Workspaces" workspaces_list && ret=0
          ;;
        add)
          _message 'Write the name of your workspace' && ret=0
          ;;
        activate)
          _describe -t rosws_workspaces "Workspaces" workspaces_list && ret=0
          ;;
        distro)
          _describe -t ros_distros "ROS 2 Distros" ros_distros && ret=0
          ;;
        show)
          _describe -t rosws_workspaces "Workspaces" workspaces_list && ret=0
          ;;
        path)
          _describe -t rosws_workspaces "Workspaces" workspaces_list && ret=0
          ;;
        cd)
          _describe -t rosws_workspaces "Workspaces" workspaces_list && ret=0
          ;;
      esac
      ;;
    third_arg)
      case $command in
        add)
          _describe -t ros_distros "ROS 2 Distros" ros_distros && ret=0
          ;;
        cd)
          # Autocomplete directories with cd command if workspace is valid
          if [[ -v rosws_workspaces[$ws_name] ]]; then
            # complete sub directories from the workspace
            _path_files -W "(${rosws_workspaces[$ws_name]})" -/ && ret=0
          fi
          ;;
      esac
      ;;
  esac

  return $ret
}

_rosws "$@"
